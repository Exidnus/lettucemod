plugins {
    id 'org.springframework.boot' version '2.5.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.github.breadmoirai.github-release' version '2.2.12'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'jacoco'
}

group = 'com.redislabs'
description = 'Java client for Redis Labs modules'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
    mavenLocal()
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

dependencies {
    api 'io.lettuce:lettuce-core:6.2.0.BUILD-SNAPSHOT'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.2'
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    testImplementation 'com.redislabs:testcontainers-redis:1.1.4-SNAPSHOT'
    testImplementation 'org.testcontainers:junit-jupiter:1.15.3'
    testImplementation 'org.slf4j:slf4j-simple'
    testImplementation 'org.apache.commons:commons-collections4:4.4'
    testImplementation 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

test {
    useJUnitPlatform()
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Mesclun'
                description = 'Java client for Redis Labs module'
                url = 'https://github.com/redis-developer/mesclun'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jruaux'
                        name = 'Julien Ruaux'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/redis-developer/mesclun.git'
                    developerConnection = 'scm:git:git@github.com:redis-developer/mesclun.git'
                    url = 'https://github.com/redis-developer/mesclun'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

githubRelease {
    token project.hasProperty('github.token') ? project.property('github.token') : ''
    owner "redis-developer"
    repo "mesclun"
    body changelog()
    tagName "${project.version}"
}

def isNonStable = { String version ->
  def nonStableKeyword = ['PREVIEW'].any { it -> version.toUpperCase().contains(it) }
  def stableKeyword = ['RELEASE', 'FINAL', 'GA', 'JRE8'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+([.-]r)?$/
  return nonStableKeyword || (!stableKeyword && !(version ==~ regex))
}

tasks.named("dependencyUpdates").configure {

  rejectVersionIf {
    isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
  }

}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

release {
    buildTasks = ['clean', 'build', 'publishToSonatype', 'closeAndReleaseSonatypeStagingRepository', 'githubRelease', 'publishToMavenLocal']
}
