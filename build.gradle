plugins {
    id 'java-library'
    id 'java-library-distribution'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id 'org.jreleaser' version '0.4.0'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

group = 'com.redislabs'
description = 'Java client for Redis Labs modules'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/redis-developer/registry")
        credentials {
            username = project.findProperty("github.username") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    api 'io.lettuce:lettuce-core:6.2.0.BUILD-SNAPSHOT-REDISLABS'
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.2'
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.12.3'
    testImplementation 'com.redislabs:testcontainers-redis:1.1.5'
    testImplementation 'org.testcontainers:junit-jupiter:1.15.3-REDISLABS'
    testImplementation 'org.slf4j:slf4j-simple:1.7.30'
    testImplementation 'org.apache.commons:commons-collections4:4.4'
    testImplementation 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
}

test {
    useJUnitPlatform()
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Mesclun'
                description = 'Java client for Redis Labs module'
                url = 'https://github.com/redis-developer/mesclun'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jruaux'
                        name = 'Julien Ruaux'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/redis-developer/mesclun.git'
                    developerConnection = 'scm:git:git@github.com:redis-developer/mesclun.git'
                    url = 'https://github.com/redis-developer/mesclun'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

def isNonStable = { String version ->
  def nonStableKeyword = ['PREVIEW'].any { it -> version.toUpperCase().contains(it) }
  def stableKeyword = ['RELEASE', 'FINAL', 'GA', 'JRE8'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+([.-]r)?$/
  return nonStableKeyword || (!stableKeyword && !(version ==~ regex))
}

tasks.named("dependencyUpdates").configure {

  rejectVersionIf {
    isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
  }

}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

nexusPublishing {
    repositories {
        sonatype()
    }
}

jreleaser {
    project {
        name = 'mesclun'
        description = 'Redis modules client'
        longDescription = 'Java client for Redis modules based on Lettuce'
        website = 'https://github.com/redis-developer/mesclun'
        authors = ['Julien Ruaux']
        tags = ['redis', 'modules', 'java', 'lettuce']
        license = 'Apache-2.0'
    }
    release {
        github {
            username = 'jruaux'
            owner = 'redis-developer'
            overwrite = true
        }
    }
}
